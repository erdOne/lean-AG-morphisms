import morphisms.universally_closed
import ring_theory.valuation.valuation_ring
import for_mathlib.valuation_subring
import morphisms.separated
import algebraic_geometry.properties

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universes v u

namespace algebraic_geometry

variables {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

open category_theory.morphism_property
open algebraic_geometry.morphism_property (topologically)

structure valuative_comm_sq {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :=
(R : Type.{u})
[hR : comm_ring R]
[hR‚ÇÅ : is_domain R]
[hR‚ÇÇ : valuation_ring R]
(K : Type.{u})
[hK : field K]
[hRK : algebra R K]
[hRK' : is_fraction_ring R K]
(i‚ÇÅ : Scheme.Spec.obj (op $ CommRing.of K) ‚ü∂ X)
(i‚ÇÇ : Scheme.Spec.obj (op $ CommRing.of R) ‚ü∂ Y)
(comm_sq : comm_sq i‚ÇÅ (Scheme.Spec.map (CommRing.of_hom $ algebra_map R K).op) f i‚ÇÇ)
.
def valuative_criterion.existence : morphism_property Scheme :=
Œª X Y f, ‚àÄ S : valuative_comm_sq f, S.comm_sq.has_lift

def valuative_criterion.uniqueness : morphism_property Scheme :=
Œª X Y f, ‚àÄ S : valuative_comm_sq f, subsingleton S.comm_sq.lift_struct

def valuative_criterion : morphism_property Scheme :=
Œª X Y f, ‚àÄ S : valuative_comm_sq f, nonempty (unique (S.comm_sq.lift_struct))

section existence

lemma valuative_criterion.existence.specializing_map (H : valuative_criterion.existence f) :
  specializing_map f.1.base :=
begin
  rintros x y (h : f.1.base x ‚§≥ y),
  let œï := Y.presheaf.stalk_specializes h ‚â´ PresheafedSpace.stalk_map f.1 x ‚â´ X.stalk_residue x,
  obtain ‚ü®A, hA, hA'‚ü© := exists_factor_valuation_ring œï,
  let œï' := œï.cod_restrict A.to_subring hA,
  have : CommRing.of_hom œï' ‚â´ CommRing.of_hom (algebra_map ‚Ü•A ‚Ü•(X.residue_field x)) = œï,
  { ext, refl },
  obtain ‚ü®‚ü®H'‚ü©‚ü© := H ‚ü®A, X.residue_field x, X.from_Spec_residue_field x,
    Scheme.Spec.map (CommRing.of_hom œï').op ‚â´ Y.from_Spec_stalk y, ‚ü®_‚ü©‚ü©,
  refine ‚ü®H'.l.1.base (local_ring.closed_point A), _, _‚ü©,
  { simp only [‚Üê functor.map_comp_assoc, ‚Üê op_comp, this, œï],
    erw op_comp,
    simp only [op_comp, functor.map_comp_assoc],
    erw Scheme.stalk_specializes_from_Spec_stalk h,
    rw [stalk_map_from_Spec_stalk, ‚Üê category.assoc], refl },
  { show local_ring A, by apply_instance },
  { change _ ‚§≥ _,
    conv_lhs { rw [‚Üê Scheme.from_Spec_residue_field_base x (‚ä• : prime_spectrum $ X.residue_field x),
      ‚Üê (show _ = X.from_Spec_residue_field x, from H'.fac_left)] },
    refine specializes.map _ H'.l.1.base.2,
    apply_with local_ring.specializes_closed_point { instances := ff } },
  { rw [‚Üê Scheme.comp_val_base_apply, H'.fac_right],
    dsimp only,
    erw Scheme.comp_val_base_apply,
    convert Scheme.from_Spec_stalk_closed_point _,
    apply_with local_ring.comap_closed_point { instances := ff },
    exact hA' }
end

-- move me
lemma _root_.category_theory.functor.preimage_injective {C D} [category C] [category D]
  (F : C ‚•§ D) [full F] {X Y : C} : 
  function.injective (F.preimage : _ ‚Üí (X ‚ü∂ Y)) :=
Œª f g e, by rw [‚Üê F.image_preimage f, ‚Üê F.image_preimage g, e]

lemma valuative_criterion.existence.of_specializing_map
  (H : universally (topologically @specializing_map) f) :
  valuative_criterion.existence f :=
begin
  rintros ‚ü®R, K, i‚ÇÅ, i‚ÇÇ, c‚ü©,
  resetI,
  let X' := pullback f i‚ÇÇ,
  let S := Scheme.Spec.obj (op $ CommRing.of R),
  let f' : X' ‚ü∂ S := pullback.snd,
  let i‚ÇÅ' : _ ‚ü∂ X' := pullback.lift i‚ÇÅ _ c.1,
  let x' : X'.carrier := i‚ÇÅ'.1.base (show prime_spectrum K, from local_ring.closed_point _),
  let s' : S.carrier := (show prime_spectrum R, from local_ring.closed_point _),
  have hxs : f'.1.base x' ‚§≥ s' := local_ring.specializes_closed_point _,
  have hf' : specializing_map f'.1.base := H _ _ _ (is_pullback.of_has_pullback _ _).flip,
  obtain ‚ü®x, hx : _ ‚§≥ _, e‚ü© := hf' hxs,
  let œï : CommRing.of R ‚ü∂ X'.stalk x := structure_sheaf.to_stalk R s' ‚â´
    S.presheaf.stalk_specializes (specializes_of_eq e) ‚â´ PresheafedSpace.stalk_map f'.1 x,
  haveI : is_local_ring_hom œï := by apply is_local_ring_hom_comp,
  let œà : X'.presheaf.stalk x ‚ü∂ CommRing.of K :=
    X'.presheaf.stalk_specializes hx ‚â´ stalk_closed_point_to _ i‚ÇÅ',
  have hœï : œï ‚â´ œà = CommRing.of_hom (algebra_map R K),
  { simp only [œï, stalk_closed_point_to, category.assoc,
      ‚Üê PresheafedSpace.stalk_map.stalk_specializes_stalk_map_assoc,
      Top.presheaf.stalk_specializes_comp_assoc],
    slice_lhs 3 4 { erw ‚Üê PresheafedSpace.stalk_map.comp },
    have : i‚ÇÅ'.val ‚â´ f'.val = (Scheme.Spec.map (CommRing.of_hom (algebra_map R K)).op).1 := 
      congr_arg LocallyRingedSpace.hom.val (pullback.lift_snd i‚ÇÅ _ c.1),
    erw [PresheafedSpace.stalk_map.congr_hom' _ _ this],
    simp only [category.assoc, Top.presheaf.stalk_specializes_comp_assoc],
    erw structure_sheaf.to_stalk_stalk_specializes_assoc,
    slice_lhs 1 2 { erw stalk_map_to_stalk },
    rw iso.comp_inv_eq,
    simp_rw category.assoc,
    erw structure_sheaf.to_stalk_comp_stalk_to_fiber_ring_hom,
    refl },
  have hœà := @bijective_range_restrict_comp_of_valuation_ring R (X'.presheaf.stalk x) K
    _ _ _ _ _ _ _ _ _ _ hœï _,
  let œà' : _ ‚ü∂ CommRing.of R :=
    (ring_equiv.of_bijective _ hœà).symm.to_ring_hom.comp œà.range_restrict,
  haveI : is_local_ring_hom œà',
  { apply_with is_local_ring_hom_comp { instances := ff },
    { exact is_local_ring_hom_equiv (ring_equiv.of_bijective _ hœà).symm },
    { exact is_local_ring_hom_of_surjective _ œà.range_restrict_surjective } },
  have hœà'' : œï ‚â´ œà' = ùüô _, 
  { ext1 y, exact (ring_equiv.of_bijective _ hœà).symm_apply_apply y },
  haveI : mono (CommRing.of_hom (algebra_map R K)),
  { apply functor.mono_of_mono_map (forget CommRing), rw mono_iff_injective,
    exact (is_fraction_ring.injective R K : _) },
  have hœà' : œà' ‚â´ CommRing.of_hom (algebra_map R K) = œà, 
  { rw ‚Üê hœï, ext1 y,
    change ((ring_equiv.of_bijective _ hœà) $ (ring_equiv.of_bijective _ hœà).symm _).1 = _,
    rw ring_equiv.apply_symm_apply, refl },
  haveI : local_ring (CommRing.of R) := show local_ring R, by apply_instance,
  refine ‚ü®‚ü®‚ü®(Spec_to_equiv_of_local_ring (CommRing.of R) _).symm ‚ü®_, œà', infer_instance‚ü©
    ‚â´ pullback.fst, _, _‚ü©‚ü©‚ü©,
  { dsimp only,
    transitivity i‚ÇÅ' ‚â´ pullback.fst, swap, { exact pullback.lift_fst _ _ _ },
    rw ‚Üê category.assoc, congr' 1,
    refine (functor.map_comp_assoc _ _ _ _).symm.trans _,
    dsimp only,
    rw [‚Üê op_comp, hœà', op_comp, functor.map_comp_assoc, Scheme.stalk_specializes_from_Spec_stalk],
    exact Spec_map_stalk_closed_point_to_from_stalk _ _ },
  { dsimp only,
    rw [category.assoc, pullback.condition, ‚Üê category.assoc],
    convert category.id_comp _,
    show (_ ‚â´ _) ‚â´ f' = ùüô _,
    apply Scheme.Spec.preimage_injective,
    rw ‚Üê cancel_epi (CommRing.of_hom (algebra_map R K)).op,
    apply Scheme.Spec.map_injective,
    simp only [functor.map_comp, op_comp, functor.image_preimage, category.assoc],
    rw [‚Üê functor.map_comp_assoc, ‚Üê op_comp, hœà', op_comp, functor.map_comp_assoc,
      Scheme.stalk_specializes_from_Spec_stalk_assoc],
    erw Spec_map_stalk_closed_point_to_from_stalk_assoc,
    rw [pullback.lift_snd, category.comp_id] }
end
.
lemma valuative_criterion.existence_stable_under_base_change : 
  valuative_criterion.existence.stable_under_base_change :=
begin
  rintros X Y Y' S f g h k H hg ‚ü®R, K, i‚ÇÑ, i‚ÇÇ, c‚ü©,
  resetI,
  obtain ‚ü®‚ü®‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü©‚ü©‚ü© := hg ‚ü®R, K, i‚ÇÑ ‚â´ h, i‚ÇÇ ‚â´ f, ‚ü®_‚ü©‚ü©,
  obtain ‚ü®l', hl‚ÇÉ, hl‚ÇÑ‚ü© := pullback_cone.is_limit.lift' H.is_limit l i‚ÇÇ hl‚ÇÇ,
  refine ‚ü®‚ü®‚ü®l', _, hl‚ÇÑ‚ü©‚ü©‚ü©,
  apply pullback_cone.is_limit.hom_ext H.is_limit,
  { simp only [category.assoc, hl‚ÇÉ, hl‚ÇÅ], refl },
  { simp only [category.assoc, hl‚ÇÑ, c.w.symm], refl },
  { simp only [category.assoc, ‚Üê c.w_assoc, H.w] }
end

lemma valuative_criterion.existence_eq :
  valuative_criterion.existence = universally (topologically @specializing_map) :=
begin
  apply le_antisymm,
  { rw ‚Üê valuative_criterion.existence_stable_under_base_change.universally_eq,
    exact universally_mono (Œª X Y f, valuative_criterion.existence.specializing_map f) },
  { exact Œª X Y f, valuative_criterion.existence.of_specializing_map f }
end

lemma universally_closed_eq_valuative_criterion : 
  @universally_closed = @quasi_compact ‚äì valuative_criterion.existence :=
by rw [valuative_criterion.existence_eq,
  universally_closed_eq_quasi_compact_and_universally_specializing]

lemma universally_closed_of_valuative_criterion [quasi_compact f]
  (hf : valuative_criterion.existence f) : universally_closed f :=
begin
  rw universally_closed_eq_valuative_criterion,
  exact ‚ü®infer_instance, hf‚ü©
end


end existence

section uniqueness

lemma separated_of_valuative_criterion [quasi_separated f]
  (hf : valuative_criterion.uniqueness f) : separated f :=
begin
  suffices : universally_closed (pullback.diagonal f),
  { constructor,
    apply is_closed_immersion.of_is_immersion,
    exactI (universally_closed.is_closed_map $ pullback.diagonal f).closed_range },
  apply universally_closed_of_valuative_criterion,
  rintro ‚ü®R, K, i‚ÇÅ, i‚ÇÇ, c‚ü©,
  resetI,
  have c' : comm_sq i‚ÇÅ (Scheme.Spec.map (CommRing.of_hom (algebra_map R K)).op) f
    (i‚ÇÇ ‚â´ pullback.fst ‚â´ f),
  { constructor, rw [‚Üê c.w_assoc, pullback.diagonal_fst_assoc] },
  have : i‚ÇÇ ‚â´ pullback.fst = i‚ÇÇ ‚â´ pullback.snd,
  { injection @@subsingleton.elim (hf ‚ü®R, K, i‚ÇÅ, i‚ÇÇ ‚â´ pullback.fst ‚â´ f, c'‚ü©)
      ‚ü®i‚ÇÇ ‚â´ pullback.fst, _, category.assoc _ _ _‚ü© ‚ü®i‚ÇÇ ‚â´ pullback.snd, _, _‚ü©; dsimp only,
    { rw [‚Üê c.w_assoc, pullback.diagonal_fst, category.comp_id] },
    { rw [‚Üê c.w_assoc, pullback.diagonal_snd, category.comp_id] },
    { rw [category.assoc, pullback.condition] } },
  refine ‚ü®‚ü®‚ü®i‚ÇÇ ‚â´ pullback.fst, _, _‚ü©‚ü©‚ü©; dsimp only,
  { rw [‚Üê c.w_assoc, pullback.diagonal_fst, category.comp_id] },
  { apply pullback.hom_ext; simp only [category.assoc, pullback.diagonal_fst, pullback.diagonal_snd,
      category.comp_id, this] }
end
.

--move me
def Spec_Œì_arrow_iso_of_is_affine [is_affine X] [is_affine Y] :
  arrow.mk f ‚âÖ arrow.mk (Scheme.Spec.map (Scheme.Œì.map f.op).op) :=
arrow.iso_mk' _ _ (as_iso $ Œì_Spec.adjunction.unit.app _) (as_iso $ Œì_Spec.adjunction.unit.app _)
  (Œì_Spec.adjunction.unit_naturality f)

--move me
def Œì_Spec_arrow_iso {R S : CommRing} (f : R ‚ü∂ S) :
  arrow.mk f ‚âÖ arrow.mk (Scheme.Œì.map (Scheme.Spec.map f.op).op) :=
(arrow.iso_of_nat_iso Spec_Œì_identity (arrow.mk f)).symm

lemma separated.valuative_criterion [separated f] :
  valuative_criterion.uniqueness f :=
begin
  rintro ‚ü®R, K, i‚ÇÅ, i‚ÇÇ, c‚ü©,
  constructor,
  rintro ‚ü®l‚ÇÅ, hl‚ÇÅ, hl‚ÇÅ'‚ü© ‚ü®l‚ÇÇ, hl‚ÇÇ, hl‚ÇÇ'‚ü©,
  ext1,
  dsimp only at *,
  let h := hl‚ÇÅ'.trans hl‚ÇÇ'.symm,
  have := is_closed_immersion_stable_under_base_change
    (pullback_fst_map_snd_is_pullback f f (pullback.diagonal f)
    (pullback.lift l‚ÇÅ l‚ÇÇ h)) infer_instance,
  haveI : is_iso (pullback.diagonal f ‚â´ pullback.snd),
  { rw [pullback.diagonal_snd], apply_instance },
  rw ‚Üê is_closed_immersion_respects_iso.cancel_right_is_iso _ pullback.snd at this,
  swap, { apply_instance },
  rw [pullback.lift_snd, category.comp_id] at this,
  let Z := pullback (pullback.diagonal f) (pullback.lift l‚ÇÅ l‚ÇÇ h),
  let g : Z ‚ü∂ _ := pullback.snd,
  change is_closed_immersion g at this,
  resetI,
  haveI : is_affine Z := is_affine_of_affine g,
  have hg‚ÇÇ := ((is_closed_immersion_over_affine_iff g).mp this).2,
  suffices : is_iso g,
  { resetI, 
    refine (pullback.lift_fst l‚ÇÅ l‚ÇÇ h).symm.trans (eq.trans _ (pullback.lift_snd l‚ÇÅ l‚ÇÇ h)),
    rw [‚Üê cancel_epi g, ‚Üê pullback.condition_assoc, ‚Üê pullback.condition_assoc,
      pullback.diagonal_fst, pullback.diagonal_snd] },
  let l : Scheme.Spec.obj (op $ CommRing.of K) ‚ü∂ Z :=
    pullback.lift i‚ÇÅ (Scheme.Spec.map (CommRing.of_hom (algebra_map R K)).op) _,
  swap,
  { apply pullback.hom_ext; simp only [pullback.diagonal_fst, pullback.diagonal_snd,
      category.assoc, category.comp_id, pullback.lift_fst, pullback.lift_snd, hl‚ÇÅ, hl‚ÇÇ] },
  have hg : l ‚â´ g = Scheme.Spec.map (CommRing.of_hom (algebra_map R K)).op := pullback.lift_snd _ _ _,
  have hg‚ÇÅ := ((morphism_property.injective_respects_iso _).arrow_mk_iso_iff
    (Œì_Spec_arrow_iso $ CommRing.of_hom $ algebra_map R K)).mp (is_fraction_ring.injective R K : _),
  rw [‚Üê hg, op_comp, functor.map_comp] at hg‚ÇÅ,
  rw is_iso_respects_iso.arrow_mk_iso_iff (Spec_Œì_arrow_iso_of_is_affine g),
  convert_to is_iso
    (Scheme.Spec.map (ring_equiv.of_bijective _ ‚ü®hg‚ÇÅ.of_comp, hg‚ÇÇ‚ü©).to_CommRing_iso.hom.op),
  apply_instance
end

lemma separated_eq_valuative_criterion :
  @separated = @quasi_separated ‚äì valuative_criterion.uniqueness :=
begin
  ext X Y f, split,
  { introI H, exact ‚ü®infer_instance, separated.valuative_criterion f‚ü© },
  { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, exactI separated_of_valuative_criterion f h‚ÇÇ }
end

end uniqueness

end algebraic_geometry