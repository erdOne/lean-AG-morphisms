
/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import morphisms.open_immersion
import morphisms.closed_immersion
import for_mathlib.locally_closed
import morphisms.integral

/-!

# Locally closed immersions

A morphism of schemes is a closed immersion if the underlying map is a closed embedding, and 
the sheaf map is locally surjective.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)


/-- A morphism is a `is_closed_immersion` if the preimages of affine open sets are affine. -/
@[mk_iff]
class is_immersion (f : X ‚ü∂ Y) extends is_preimmersion f : Prop :=
(range_is_locally_closed [] : is_locally_closed (set.range f.1.base))

lemma is_immersion.base_embedding [is_immersion f] : embedding f.1.base :=
is_preimmersion.base_embedding f

instance [is_immersion f] [is_immersion g] : is_immersion (f ‚â´ g) :=
begin
  constructor,
  rw [Scheme.comp_val_base, coe_comp, set.range_comp],
  exact (is_immersion.range_is_locally_closed f).image (is_immersion.base_embedding g).to_inducing
    (is_immersion.range_is_locally_closed g)
end

instance is_open_immersion.to_is_immersion [is_open_immersion f] : is_immersion f :=
begin
  constructor,
  exact (is_open_immersion.base_open f).2.is_locally_closed
end

instance is_closed_immersion.to_is_immersion [is_closed_immersion f] : is_immersion f :=
‚ü®(is_closed_immersion.base_closed f).2.is_locally_closed‚ü© 

lemma is_closed_immersion_iff_is_immersion {f : X ‚ü∂ Y} :
  is_closed_immersion f ‚Üî is_immersion f ‚àß is_closed (set.range f.1.base) :=
‚ü®Œª H, by exactI ‚ü®infer_instance, H.2‚ü©, Œª ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, by exactI ‚ü®h‚ÇÇ‚ü©‚ü©   

lemma is_closed_immersion.of_is_immersion [is_immersion f] (hf : is_closed (set.range f.1.base)) :
  is_closed_immersion f :=
‚ü®hf‚ü©

/-- Also see `is_immersion.of_comp` in the file `morphisms/separated` -/
lemma is_immersion.of_comp_of_is_immersion [is_immersion (f ‚â´ g)] [is_immersion g] :
  is_immersion f :=
begin
  haveI := is_preimmersion.of_comp f g,
  constructor,
  rw [‚Üê set.preimage_image_eq (set.range f.1.base) (is_immersion.base_embedding g).inj,
    ‚Üê set.range_comp, ‚Üê coe_comp, ‚Üê Scheme.comp_val_base],
  exact (is_immersion.range_is_locally_closed (f ‚â´ g)).preimage g.1.base.2
end

@[simps]
def is_immersion.factor_opens [is_immersion f] : opens Y.carrier :=
‚ü®_, (is_immersion.range_is_locally_closed f).is_open_coboundary‚ü©

def is_immersion.factor_open_subscheme [is_immersion f] : Scheme :=
Y.restrict (is_immersion.factor_opens f).open_embedding

@[derive is_open_immersion]
def is_immersion.factor_open_immersion [is_immersion f] :
  is_immersion.factor_open_subscheme f ‚ü∂ Y :=
Y.of_restrict _

-- move me
lemma opens.range_inclusion {X : Top} (U : opens X) : set.range U.inclusion = U :=
subtype.range_coe

def is_immersion.factor_closed_immersion [is_immersion f] :
  X ‚ü∂ is_immersion.factor_open_subscheme f :=
is_open_immersion.lift (is_immersion.factor_open_immersion f) f
begin
  rw [is_immersion.factor_open_immersion, Scheme.of_restrict_val_base, opens.range_inclusion],
  exact subset_coboundary
end

@[simp, reassoc]
lemma is_immersion.factors [is_immersion f] :
  is_immersion.factor_closed_immersion f ‚â´ is_immersion.factor_open_immersion f = f :=
is_open_immersion.lift_fac _ _ _

-- move me
lemma is_open_immersion.range_lift (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [is_open_immersion f] 
  (H : set.range g.1.base ‚äÜ set.range f.1.base) :
  set.range (is_open_immersion.lift f g H).1.base = f.1.base ‚Åª¬π' (set.range g.1.base) :=
LocallyRingedSpace.is_open_immersion.lift_range _ _ _

instance [is_immersion f] : is_closed_immersion (is_immersion.factor_closed_immersion f) :=
begin
  haveI : is_immersion (is_immersion.factor_closed_immersion f),
  { apply_with (is_immersion.of_comp_of_is_immersion _ (is_immersion.factor_open_immersion f))
    { instances := ff },
    rwa is_immersion.factors },
  apply is_closed_immersion.of_is_immersion,
  rw [is_immersion.factor_closed_immersion, is_open_immersion.range_lift],
  exact is_closed_preimage_coe_coboundary
end

lemma is_immersion_iff_exists_factor : 
  is_immersion f ‚Üî ‚àÉ {Z : Scheme} (g : X ‚ü∂ Z) [is_closed_immersion g]
    (h : Z ‚ü∂ Y) [is_open_immersion h], g ‚â´ h = f :=
begin
  split,
  { introI H, exact ‚ü®_, is_immersion.factor_closed_immersion f, infer_instance,
      is_immersion.factor_open_immersion f, infer_instance, is_immersion.factors f‚ü© },
  { rintro ‚ü®_, _, _, _, _, rfl‚ü©, by exactI infer_instance }
end

instance is_immersion.to_mono [is_immersion f] : mono f :=
by { rw ‚Üê is_immersion.factors f, apply mono_comp }

instance is_immersion.to_locally_of_finite_type [is_immersion f] : locally_of_finite_type f :=
by { rw ‚Üê is_immersion.factors f, apply_instance }

lemma is_immersion_stable_under_composition : 
  morphism_property.stable_under_composition @is_immersion :=
Œª _ _ _ _ _ _ _, by exactI infer_instance

lemma is_immersion_respects_iso : 
  morphism_property.respects_iso @is_immersion :=
is_immersion_stable_under_composition.respects_iso (Œª _ _ _, infer_instance)

lemma is_immersion_stable_under_base_change :
  morphism_property.stable_under_base_change @is_immersion :=
begin
  intros X Y X' S f g f' g' H hg,
  rw is_immersion_iff_exists_factor at hg ‚ä¢,
  obtain ‚ü®Z, i‚ÇÅ, _, i‚ÇÇ, _, rfl‚ü© := hg,
  resetI,
  refine ‚ü®pullback i‚ÇÇ f, pullback.lift (f' ‚â´ i‚ÇÅ) g' ((category.assoc _ _ _).trans H.w), _,
    pullback.snd, infer_instance, pullback.lift_snd _ _ _‚ü©,
  have : is_pullback f'
    (pullback.lift (f' ‚â´ i‚ÇÅ) g' ((category.assoc _ _ _).trans H.w)) i‚ÇÅ pullback.fst :=
    is_pullback.of_bot (by rwa pullback.lift_snd)
      (pullback.lift_fst _ _ _).symm (is_pullback.of_has_pullback _ _),
  exact is_closed_immersion_stable_under_base_change this infer_instance
end

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [is_immersion g] :
  is_immersion (pullback.fst : pullback f g ‚ü∂ X) :=
is_immersion_stable_under_base_change.fst f g infer_instance

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [is_immersion f] :
  is_immersion (pullback.snd : pullback f g ‚ü∂ Y) :=
is_immersion_stable_under_base_change.snd f g infer_instance

lemma is_immersion_is_local_at_target :
  property_is_local_at_target @is_immersion :=
begin
  constructor,
  { exact is_immersion_respects_iso },
  { intros X Y f U hU,
    haveI := is_preimmersion_is_local_at_target.2 f U hU.1,
    constructor,
    rw [morphism_restrict_val_base, set.range_restrict_preimage],
    exact (is_immersion.range_is_locally_closed f).preimage continuous_subtype_coe },
  { introsI X Y f ùí∞ H,
    haveI := is_preimmersion_is_local_at_target.3 f ùí∞ infer_instance,
    constructor,
    apply (is_locally_closed_iff_coe_preimage_of_supr_eq_top ùí∞.supr_opens_range _).mpr,
    intro i,
    convert ((is_immersion_respects_iso.arrow_mk_iso_iff
      (morphism_restrict_opens_range f (ùí∞.map i))).mpr (H i)).2 using 1,
    rw [morphism_restrict_val_base, set.range_restrict_preimage] },
end

lemma is_immersion_open_cover_tfae (f : X ‚ü∂ Y) :
  tfae [is_immersion f,
    ‚àÉ (ùí∞ : Scheme.open_cover.{u} Y), ‚àÄ (i : ùí∞.J),
      is_immersion (pullback.snd : (ùí∞.pullback_cover f).obj i ‚ü∂ ùí∞.obj i),
    ‚àÄ (ùí∞ : Scheme.open_cover.{u} Y) (i : ùí∞.J),
      is_immersion (pullback.snd : (ùí∞.pullback_cover f).obj i ‚ü∂ ùí∞.obj i),
    ‚àÄ (U : opens Y.carrier), is_immersion (f ‚à£_ U),
    ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [is_open_immersion g],
      is_immersion (pullback.snd : pullback f g ‚ü∂ U),
    ‚àÉ {Œπ : Type u} (U : Œπ ‚Üí opens Y.carrier) (hU : supr U = ‚ä§), (‚àÄ i, is_immersion (f ‚à£_ (U i)))] :=
is_immersion_is_local_at_target.open_cover_tfae f

-- lemma is_immersion_open_cover_of_subset_supr (f : X ‚ü∂ Y)
--   {Œπ : Type u} (U : Œπ ‚Üí opens Y.carrier) (hU : set.range f.1.base ‚äÜ ((supr U : _) : set Y.carrier))
--   (h : ‚àÄ i, is_immersion (f ‚à£_ U i)) : is_immersion f :=
-- begin
--   let V := supr U,
--   have hV : (opens.map f.val.base).obj V = ‚ä§,
--   { rw [eq_top_iff], exact set.image_subset_iff.mp ((subset_of_eq set.image_univ).trans hU) },
--   suffices : is_immersion (f ‚à£_ V),
--   { haveI : is_iso (X.of_restrict ((opens.map f.val.base).obj V).open_embedding),
--     { rw hV,
--       apply_with (is_iso_of_reflects_iso _ Scheme.forget_to_LocallyRingedSpace) { instances := ff },
--       convert_to is_iso X.to_LocallyRingedSpace.restrict_top_iso.hom,
--       apply_instance },
--     rw [‚Üê is_immersion_respects_iso.cancel_left_is_iso
--       (X.of_restrict ((opens.map f.val.base).obj V).open_embedding), ‚Üê morphism_restrict_Œπ],
--     apply_instance },
--   let U' : Œπ ‚Üí opens (Y.restrict V.open_embedding).carrier := (opens.map V.inclusion).obj ‚àò U,
--   have : supr U' = ‚ä§,
--   { rw [eq_top_iff],
--     rintro ‚ü®x, hx‚ü© -,
--     obtain ‚ü®i, hi‚ü© := opens.mem_supr.mp hx,
--     exact opens.mem_supr.mpr ‚ü®i, hi‚ü© },
--   refine ((is_immersion_open_cover_tfae (f ‚à£_ V)).out 0 5).mpr ‚ü®_, U', this, Œª i, _‚ü©,
--   refine (is_immersion_respects_iso.arrow_iso_iff (morphism_restrict_restrict f _ _ ‚â™‚â´
--     morphism_restrict_eq f ((V.functor_map_eq_inf _).trans _))).mpr (h i),
--   rw inf_eq_left,
--   exact le_supr U i,
-- end

lemma is_closed_immersion_pullback_diagonal_Spec {R S : CommRing}
  (f' : Scheme.Spec.obj (op S) ‚ü∂ Scheme.Spec.obj (op R)) :
  is_closed_immersion (pullback.diagonal f') :=
begin
  let f : R ‚ü∂ S := (Scheme.Spec.preimage f').unop,
  have : Scheme.Spec.map f.op = f', { rw [quiver.hom.op_unop, functor.image_preimage] },
  clear_value f, subst this,
  letI := f.to_algebra,
  have H := (is_pushout.of_is_colimit
    (CommRing.pushout_cocone_is_colimit f f)).op.map Scheme.Spec,
  let e : pullback.diagonal_obj (Scheme.Spec.map f.op) ‚âÖ 
    Scheme.Spec.obj (op (CommRing.pushout_cocone f f).X) := limit.iso_limit_cone ‚ü®_, H.is_limit‚ü©,
  have : pullback.diagonal _ ‚â´ e.hom = Scheme.Spec.map
    (quiver.hom.op $ (@algebra.tensor_product.lmul' R S _ _ _).to_ring_hom),
  { apply pullback_cone.is_limit.hom_ext H.is_limit,
    { rw category.assoc,
      erw [is_limit.cone_point_unique_up_to_iso_hom_comp, pullback.diagonal_fst],
      rw [H.cone_fst, ‚Üê functor.map_comp, ‚Üê op_comp],
      transitivity Scheme.Spec.map (ùüô _).op, { rw [op_id, category_theory.functor.map_id] },
      congr' 2, ext1, simp },
    { rw category.assoc,
      erw [is_limit.cone_point_unique_up_to_iso_hom_comp, pullback.diagonal_snd],
      rw [H.cone_snd, ‚Üê functor.map_comp, ‚Üê op_comp],
      transitivity Scheme.Spec.map (ùüô _).op, { rw [op_id, category_theory.functor.map_id] },
      congr' 2, ext1, simp }, }, 
  rw [‚Üê is_closed_immersion_respects_iso.cancel_right_is_iso _ e.hom, this,
    is_closed_immersion_Spec_iff],
  exact Œª x, ‚ü®x ‚äó‚Çú 1, (algebra.tensor_product.lmul'_apply_tmul _ _).trans (mul_one _)‚ü©,
end

-- -- move me
-- abbreviation Scheme.hom.lift_open (U : opens Y.carrier) (hU : set.range f.1.base ‚äÜ U.1) :
--   X ‚ü∂ Y.restrict U.open_embedding := 
-- is_open_immersion.lift (Y.of_restrict U.open_embedding) f
--   (hU.trans $ subset_of_eq $ subtype.range_coe.symm)

-- lemma Scheme.hom.lift_open_of_restrict (U : opens Y.carrier) (hU : set.range f.1.base ‚äÜ U.1) :
--   X.of_restrict _ ‚â´ f.lift_open U hU = f ‚à£_ U :=  
-- begin
--   rw ‚Üê cancel_mono (Y.of_restrict U.open_embedding),
--   rw [category.assoc, is_open_immersion.lift_fac, morphism_restrict_Œπ],
-- end

variable {f}

variables (ùí∞ : Scheme.open_cover.{u} Y) (ùí± : ‚àÄ i, Scheme.open_cover.{u} (pullback f (ùí∞.map i)))

def pullback.diagonal_cover :
  (pullback.diagonal_obj f).open_cover :=
(Scheme.pullback.open_cover_of_base ùí∞ f f).bind
  (Œª i, Scheme.pullback.open_cover_of_left_right (ùí± i) (ùí± i) pullback.snd pullback.snd)

def pullback.diagonal_cover_diagonal :
  opens (pullback.diagonal_obj f).carrier :=
‚®Ü i : Œ£ i, (ùí± i).J, ((pullback.diagonal_cover ùí∞ ùí±).map ‚ü®i.1, i.2, i.2‚ü©).opens_range

lemma pullback.diagonal_cover_map_eq (I) : (pullback.diagonal_cover ùí∞ ùí±).map I =
  pullback.map _ _ _ _
    ((ùí± I.fst).map _ ‚â´ pullback.fst) ((ùí± I.fst).map _ ‚â´ pullback.fst) (ùí∞.map _)
    (by simp only [category.assoc, pullback.condition])
    (by simp only [category.assoc, pullback.condition]) :=
begin
  dsimp [pullback.diagonal_cover],
  apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc,
      pullback.diagonal_fst_assoc, pullback.diagonal_fst,
      pullback.diagonal_snd_assoc, pullback.diagonal_snd, category.comp_id]
end

lemma pullback.diagonal_cover_diagonal_eq_top_of_injective (hf : function.injective f.1.base) :
  pullback.diagonal_cover_diagonal ùí∞ ùí± = ‚ä§ :=
begin
  rw eq_top_iff,
  rintro x -, 
  let x‚ÇÅ := (pullback.fst : pullback.diagonal_obj f ‚ü∂ X).1.base x,
  let x‚ÇÇ := (pullback.snd : pullback.diagonal_obj f ‚ü∂ X).1.base x,
  have hx : x‚ÇÅ = x‚ÇÇ,
  { apply hf, simp_rw [‚Üê Scheme.comp_val_base_apply, pullback.condition] },
  let i := ùí∞.f (f.1.base x‚ÇÅ),
  obtain ‚ü®t, ht‚ü© : x‚ÇÅ ‚àà set.range (pullback.fst : pullback f (ùí∞.map i) ‚ü∂ _).1.base,
  { rw is_open_immersion.range_pullback_fst_of_right, exact (ùí∞.covers $ f.1.base x‚ÇÅ) },
  obtain ‚ü®w, hw‚ü© := (ùí± i).covers t,
  rw ‚Üê hw at ht,
  refine opens.mem_supr.mpr ‚ü®‚ü®i, (ùí± i).f t‚ü©, _‚ü©,
  change _ ‚àà set.range ((pullback.diagonal_cover ùí∞ ùí±).map ‚ü®i, ((ùí± i).f t, (ùí± i).f t)‚ü©).1.base,
  simp_rw [pullback.diagonal_cover_map_eq ùí∞ ùí±, pullback.range_map],
  split,
  { exact ‚ü®w, ht‚ü© },
  { rw hx at ht, exact ‚ü®w, ht‚ü© }
end
  
lemma pullback.diagonal_range_subset_supr_diagonal_cover :
  set.range (pullback.diagonal f).1.base ‚äÜ (pullback.diagonal_cover_diagonal ùí∞ ùí±).1 :=
begin
  rintros _ ‚ü®x, rfl‚ü©,
  let i := ùí∞.f (f.1.base x),
  obtain ‚ü®y, hy : (ùí∞.map i).1.base y = _‚ü© := ùí∞.covers (f.1.base x),
  let T : pullback.triplet f (ùí∞.map i) := ‚ü®x, y, _, rfl, hy‚ü©,
  obtain ‚ü®z, (hzx : _ = x), (rfl : _ = y)‚ü© := T.exists_preimage,
  obtain ‚ü®w, hw‚ü© := (ùí± i).covers z,
  rw [‚Üê hzx, ‚Üê hw],
  refine opens.mem_supr.mpr ‚ü®‚ü®i, (ùí± i).f z‚ü©,
    (pullback.diagonal ((ùí± i).map ((ùí± i).f z) ‚â´ pullback.snd)).1.base w, _‚ü©,
  simp_rw [‚Üê Scheme.comp_val_base_apply],
  congr' 3,
  dsimp [pullback.diagonal_cover],
  apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc,
      pullback.diagonal_fst_assoc, pullback.diagonal_fst,
      pullback.diagonal_snd_assoc, pullback.diagonal_snd, category.comp_id],
end

def pullback.diagonal_restrict_iso_diagonal (i j) :
  arrow.mk (pullback.diagonal f ‚à£_ ((pullback.diagonal_cover ùí∞ ùí±).map ‚ü®i, j, j‚ü©).opens_range) ‚âÖ
    arrow.mk (pullback.diagonal ((ùí± i).map j ‚â´ pullback.snd)) :=
begin
  refine (morphism_restrict_opens_range _ _) ‚â™‚â´ arrow.iso_mk _ _ _,
  { dsimp [pullback.diagonal_cover], 
    refine pullback.congr_hom rfl _ ‚â™‚â´
      pullback_diagonal_map_iso f (ùí∞.map i) ((ùí± i).map j) ((ùí± i).map j) ‚â™‚â´
      as_iso pullback.fst,
    apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
        pullback.lift_fst_assoc, pullback.lift_snd_assoc],
      },
  { dsimp [pullback.diagonal_cover], exact iso.refl _ },
  { have : (pullback.fst : pullback ((ùí± i).map j) ((ùí± i).map j) ‚ü∂ _) = pullback.snd,
    { rw ‚Üê cancel_epi (pullback.diagonal $ (ùí± i).map j),
      rw [pullback.diagonal_fst, pullback.diagonal_snd] },
    dsimp [pullback.diagonal_cover],
    apply pullback.hom_ext,
    swap, simp only [this],
    all_goals { simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc, category.id_comp, category.comp_id,
      pullback_diagonal_map_iso_hom_fst, pullback_diagonal_map_iso_hom_snd,
      pullback.diagonal_fst, pullback.diagonal_snd] } }
end

lemma pullback.is_closed_immersion_lift_diagonal_aux
  (H : ‚àÄ i j, is_closed_immersion (pullback.diagonal ((ùí± i).map j ‚â´ pullback.snd))) :
  is_closed_immersion (pullback.diagonal f ‚à£_ pullback.diagonal_cover_diagonal ùí∞ ùí±) :=
begin
  rw (is_closed_immersion.open_cover_tfae (pullback.diagonal
    f ‚à£_ pullback.diagonal_cover_diagonal ùí∞ ùí±)).out 0 5,
  have : (‚®Ü i : Œ£ i, (ùí± i).J, ((pullback.diagonal_cover ùí∞ ùí±).map ‚ü®i.1, i.2, i.2‚ü©).opens_range) =
    pullback.diagonal_cover_diagonal ùí∞ ùí± := rfl,
  apply_fun (opens.map (pullback.diagonal_cover_diagonal ùí∞ ùí±).inclusion).obj at this,
  rw [opens.map_supr, opens.inclusion_map_eq_top] at this,
  refine ‚ü®_, _, this, _‚ü©,
  rintros ‚ü®i, j‚ü©,
  rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (morphism_restrict_restrict _ _ _),
  rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (morphism_restrict_eq _ $ (opens.functor_map_eq_inf _ _).trans (inf_eq_left.mpr _)),
  { rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (pullback.diagonal_restrict_iso_diagonal ùí∞ ùí± i j), apply H },
  { exact le_supr _ _ }
end

variable (f)

lemma pullback.is_closed_immersion_lift_diagonal :
  is_closed_immersion (pullback.diagonal f ‚à£_ pullback.diagonal_cover_diagonal
    Y.affine_cover (Œª _, Scheme.affine_cover _)) :=
begin
  apply pullback.is_closed_immersion_lift_diagonal_aux,
  intros i j, apply is_closed_immersion_pullback_diagonal_Spec
end


local attribute [irreducible] Scheme.affine_cover

def Scheme.restrict_top_iso (X : Scheme.{u}) : 
  X.restrict (opens.open_embedding $ ‚ä§) ‚âÖ X :=
{ .. X.to_LocallyRingedSpace.restrict_top_iso }

-- Declaring it as an instance adds superfluous universe variables
@[instance]
lemma _root_.category_theory.limits.pullback.diagonal.is_immersion {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
  is_immersion (pullback.diagonal f) :=
begin
  let U := (pullback.diagonal_cover_diagonal Y.affine_cover (Œª _, Scheme.affine_cover _)),
  haveI : is_closed_immersion (pullback.diagonal f ‚à£_ U) :=
    pullback.is_closed_immersion_lift_diagonal f,
  have : (opens.map (pullback.diagonal f).val.base).obj U = ‚ä§,
  { ext1, show (pullback.diagonal f).val.base ‚Åª¬π' U.1 = set.univ,
    rw [‚Üê set.univ_subset_iff, ‚Üê set.image_subset_iff, set.image_univ],
    exact pullback.diagonal_range_subset_supr_diagonal_cover _ _ },
  haveI : is_iso (X.of_restrict ((opens.map (pullback.diagonal f).val.base).obj U).open_embedding),
  { rw this, exact is_iso.of_iso X.restrict_top_iso },
  have := morphism_restrict_Œπ (pullback.diagonal f) U,
  rw [‚Üê is_iso.inv_comp_eq] at this,
  rw ‚Üê this,
  apply_instance
end
-- move me 
def morphism_restrict.top_iso : arrow.mk (f ‚à£_ ‚ä§) ‚âÖ arrow.mk f :=
arrow.iso_mk' (f ‚à£_ ‚ä§) f X.restrict_top_iso Y.restrict_top_iso (morphism_restrict_Œπ _ _).symm

-- Also see `separated_of_injective`
lemma pullback.diagonal_is_closed_immersion_of_injective {X Y : Scheme.{u}} (f : X ‚ü∂ Y)
  (hf : function.injective f.1.base) :
  is_closed_immersion (pullback.diagonal f) :=
begin
  let U := (pullback.diagonal_cover_diagonal Y.affine_cover (Œª _, Scheme.affine_cover _)),
  have hU : is_closed_immersion (pullback.diagonal f ‚à£_ U) :=
    pullback.is_closed_immersion_lift_diagonal f,
  have : U = ‚ä§ := pullback.diagonal_cover_diagonal_eq_top_of_injective _ _ hf,
  rw this at hU,
  exact (is_closed_immersion_respects_iso.arrow_mk_iso_iff (morphism_restrict.top_iso _)).mp hU
end

end algebraic_geometry
