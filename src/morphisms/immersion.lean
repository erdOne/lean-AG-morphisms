
/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import morphisms.open_immersion
import morphisms.closed_immersion
import for_mathlib.locally_closed
import morphisms.integral
import algebraic_geometry.pullback_carrier

/-!

# Locally closed immersions

A morphism of schemes is a closed immersion if the underlying map is a closed embedding, and 
the sheaf map is locally surjective.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y Z : Scheme.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)


/-- A morphism is a `is_closed_immersion` if the preimages of affine open sets are affine. -/
@[mk_iff]
class is_immersion (f : X âŸ¶ Y) : Prop :=
(base_embedding [] : embedding f.1.base)
(range_is_locally_closed [] : is_locally_closed (set.range f.1.base))
(stalk_map_surjective [] : âˆ€ x, function.surjective (PresheafedSpace.stalk_map f.1 x))

instance [is_immersion f] [is_immersion g] : is_immersion (f â‰« g) :=
begin
  constructor,
  { exact (is_immersion.base_embedding g).comp (is_immersion.base_embedding f) },
  { rw [Scheme.comp_val_base, coe_comp, set.range_comp],
    exact (is_immersion.range_is_locally_closed f).image (is_immersion.base_embedding g).to_inducing
      (is_immersion.range_is_locally_closed g) },
  { intro x, erw PresheafedSpace.stalk_map.comp f.1 g.1 x,
    exact (is_immersion.stalk_map_surjective f _).comp (is_immersion.stalk_map_surjective g _) }
end

-- move me
lemma is_open_immersion.base_open [H : is_open_immersion f] : open_embedding f.1.base :=
H.1

instance is_open_immersion.to_is_immersion [is_open_immersion f] : is_immersion f :=
begin
  constructor,
  { exact (is_open_immersion.base_open f).to_embedding },
  { exact (is_open_immersion.base_open f).2.is_locally_closed },
  { exact Î» x, (as_iso $ PresheafedSpace.stalk_map f.1 x).CommRing_iso_to_ring_equiv.surjective }
end

instance is_closed_immersion.to_is_immersion [is_closed_immersion f] : is_immersion f :=
begin
  constructor,
  { exact (is_closed_immersion.base_closed f).to_embedding },
  { exact (is_closed_immersion.base_closed f).2.is_locally_closed },
  { exact is_closed_immersion.stalk_map_surjective f }
end

lemma is_closed_immersion.of_is_immersion [is_immersion f] (hf : is_closed (set.range f.1.base)) :
  is_closed_immersion f :=
begin
  rw is_closed_immersion_iff_stalk,
  exact âŸ¨âŸ¨is_immersion.base_embedding f, hfâŸ©, is_immersion.stalk_map_surjective fâŸ©
end

-- In fact it still holds if `g` is topologically a locally closed embedding
lemma is_immersion.of_comp_of_is_immersion [is_immersion (f â‰« g)] [is_immersion g] :
  is_immersion f :=
begin
  constructor,
  { exact embedding_of_embedding_compose f.1.base.2 g.1.base.2
      (is_immersion.base_embedding $ f â‰« g) },
  { rw (is_immersion.base_embedding g).to_inducing.is_locally_closed_iff,
    refine âŸ¨_, is_immersion.range_is_locally_closed (f â‰« g), _âŸ©,
    rw [Scheme.comp_val_base, coe_comp, set.range_comp],
    exact set.preimage_image_eq (set.range f.1.base) (is_immersion.base_embedding g).inj },
  { intro x,
    have := (is_immersion.stalk_map_surjective $ f â‰« g) x,
    erw PresheafedSpace.stalk_map.comp at this,
    rw coe_comp at this,
    exact function.surjective.of_comp this }
end

@[simps]
def is_immersion.factor_opens [is_immersion f] : opens Y.carrier :=
âŸ¨_, (is_immersion.range_is_locally_closed f).is_open_coboundaryâŸ©

def is_immersion.factor_open_subscheme [is_immersion f] : Scheme :=
Y.restrict (is_immersion.factor_opens f).open_embedding

@[derive is_open_immersion]
def is_immersion.factor_open_immersion [is_immersion f] :
  is_immersion.factor_open_subscheme f âŸ¶ Y :=
Y.of_restrict _

-- move me
lemma opens.range_inclusion {X : Top} (U : opens X) : set.range U.inclusion = U :=
subtype.range_coe

def is_immersion.factor_closed_immersion [is_immersion f] :
  X âŸ¶ is_immersion.factor_open_subscheme f :=
is_open_immersion.lift (is_immersion.factor_open_immersion f) f
begin
  rw [is_immersion.factor_open_immersion, Scheme.of_restrict_val_base, opens.range_inclusion],
  exact subset_coboundary
end

@[simp, reassoc]
lemma is_immersion.factors [is_immersion f] :
  is_immersion.factor_closed_immersion f â‰« is_immersion.factor_open_immersion f = f :=
is_open_immersion.lift_fac _ _ _

-- move me
lemma is_open_immersion.range_lift (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [is_open_immersion f] 
  (H : set.range g.1.base âŠ† set.range f.1.base) :
  set.range (is_open_immersion.lift f g H).1.base = f.1.base â»Â¹' (set.range g.1.base) :=
LocallyRingedSpace.is_open_immersion.lift_range _ _ _

instance [is_immersion f] : is_closed_immersion (is_immersion.factor_closed_immersion f) :=
begin
  haveI : is_immersion (is_immersion.factor_closed_immersion f),
  { apply_with (is_immersion.of_comp_of_is_immersion _ (is_immersion.factor_open_immersion f))
    { instances := ff },
    rwa is_immersion.factors },
  apply is_closed_immersion.of_is_immersion,
  rw [is_immersion.factor_closed_immersion, is_open_immersion.range_lift],
  exact is_closed_preimage_coe_coboundary
end

lemma is_immersion_iff_exists_factor : 
  is_immersion f â†” âˆƒ {Z : Scheme} (g : X âŸ¶ Z) [is_closed_immersion g]
    (h : Z âŸ¶ Y) [is_open_immersion h], g â‰« h = f :=
begin
  split,
  { introI H, exact âŸ¨_, is_immersion.factor_closed_immersion f, infer_instance,
      is_immersion.factor_open_immersion f, infer_instance, is_immersion.factors fâŸ© },
  { rintro âŸ¨_, _, _, _, _, rflâŸ©, by exactI infer_instance }
end

instance is_immersion.to_mono [is_immersion f] : mono f :=
by { rw â† is_immersion.factors f, apply mono_comp }

instance is_immersion.to_locally_of_finite_type [is_immersion f] : locally_of_finite_type f :=
by { rw â† is_immersion.factors f, apply_instance }



lemma is_immersion_stable_under_composition : 
  morphism_property.stable_under_composition @is_immersion :=
Î» _ _ _ _ _ _ _, by exactI infer_instance

lemma is_immersion_respects_iso : 
  morphism_property.respects_iso @is_immersion :=
is_immersion_stable_under_composition.respects_iso (Î» _ _ _, infer_instance)

lemma is_immersion_stable_under_base_change :
  morphism_property.stable_under_base_change @is_immersion :=
begin
  intros X Y X' S f g f' g' H hg,
  rw is_immersion_iff_exists_factor at hg âŠ¢,
  obtain âŸ¨Z, iâ‚, _, iâ‚‚, _, rflâŸ© := hg,
  resetI,
  refine âŸ¨pullback iâ‚‚ f, pullback.lift (f' â‰« iâ‚) g' ((category.assoc _ _ _).trans H.w), _,
    pullback.snd, infer_instance, pullback.lift_snd _ _ _âŸ©,
  have : is_pullback f'
    (pullback.lift (f' â‰« iâ‚) g' ((category.assoc _ _ _).trans H.w)) iâ‚ pullback.fst :=
    is_pullback.of_bot (by rwa pullback.lift_snd)
      (pullback.lift_fst _ _ _).symm (is_pullback.of_has_pullback _ _),
  exact is_closed_immersion_stable_under_base_change this infer_instance
end

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [is_immersion g] :
  is_immersion (pullback.fst : pullback f g âŸ¶ X) :=
is_immersion_stable_under_base_change.fst f g infer_instance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [is_immersion f] :
  is_immersion (pullback.snd : pullback f g âŸ¶ Y) :=
is_immersion_stable_under_base_change.snd f g infer_instance

lemma is_immersion_is_local_at_target :
  property_is_local_at_target @is_immersion :=
begin
  constructor,
  { exact is_immersion_respects_iso },
  { introsI X Y f U H,
    constructor,
    { rw morphism_restrict_val_base, exact H.1.restrict_preimage U.1 },
    { rw [morphism_restrict_val_base, set.range_restrict_preimage],
      exact (is_immersion.range_is_locally_closed f).preimage continuous_subtype_coe },
    { exact Î» x, ((morphism_property.surjective_respects_iso _).arrow_iso_iff
      (morphism_restrict_stalk_map f U x)).mpr (H.3 x.1) } },
  { intros X Y f ğ’° H,
    constructor,
    { apply (embedding_iff_embedding_of_supr_eq_top
        ğ’°.supr_opens_range f.1.base.2).mpr,
      intro i,
      have := ((is_immersion_respects_iso.arrow_iso_iff
        (morphism_restrict_opens_range f (ğ’°.map i))).mpr (H i)).1,
      rwa [arrow.mk_hom, morphism_restrict_val_base] at this },
    { rw is_locally_closed_iff_coe_preimage_of_supr_eq_top ğ’°.supr_opens_range,
      intro i, 
      replace H := (H i).2,
      simp_rw [pullback.range_snd, (is_open_immersion.base_open $ ğ’°.map i).to_embedding
        .is_locally_closed_iff, set.image_preimage_eq_inter_range] at H,
      obtain âŸ¨s, hs, hs'âŸ© := H,
      rw embedding_subtype_coe.is_locally_closed_iff,
      refine âŸ¨s, hs, _âŸ©,
      rwa [set.image_preimage_eq_inter_range, @subtype.range_coe _ (set.range _)] },
    { exact Î» x, ((morphism_property.surjective_respects_iso _).arrow_iso_iff
        (morphism_restrict_stalk_map f _ _)).mp
        (((is_immersion_respects_iso.arrow_iso_iff (morphism_restrict_opens_range f (ğ’°.map _)))
        .mpr (H (ğ’°.f $ f.1.base x))).3 âŸ¨x, ğ’°.covers (f.1.base x)âŸ©) } }
end
.

lemma is_immersion_open_cover_tfae (f : X âŸ¶ Y) :
  tfae [is_immersion f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      is_immersion (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      is_immersion (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), is_immersion (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      is_immersion (pullback.snd : pullback f g âŸ¶ U),
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤), (âˆ€ i, is_immersion (f âˆ£_ (U i)))] :=
is_immersion_is_local_at_target.open_cover_tfae f

lemma is_immersion_open_cover_of_subset_supr (f : X âŸ¶ Y)
  {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : set.range f.1.base âŠ† ((supr U : _) : set Y.carrier))
  (h : âˆ€ i, is_immersion (f âˆ£_ U i)) : is_immersion f :=
begin
  let V := supr U,
  have hV : (opens.map f.val.base).obj V = âŠ¤,
  { rw [eq_top_iff], exact set.image_subset_iff.mp ((subset_of_eq set.image_univ).trans hU) },
  suffices : is_immersion (f âˆ£_ V),
  { haveI : is_iso (X.of_restrict ((opens.map f.val.base).obj V).open_embedding),
    { rw hV,
      apply_with (is_iso_of_reflects_iso _ Scheme.forget_to_LocallyRingedSpace) { instances := ff },
      convert_to is_iso X.to_LocallyRingedSpace.restrict_top_iso.hom,
      apply_instance },
    rw [â† is_immersion_respects_iso.cancel_left_is_iso
      (X.of_restrict ((opens.map f.val.base).obj V).open_embedding), â† morphism_restrict_Î¹],
    apply_instance },
  let U' : Î¹ â†’ opens (Y.restrict V.open_embedding).carrier := (opens.map V.inclusion).obj âˆ˜ U,
  have : supr U' = âŠ¤,
  { rw [eq_top_iff],
    rintro âŸ¨x, hxâŸ© -,
    obtain âŸ¨i, hiâŸ© := opens.mem_supr.mp hx,
    exact opens.mem_supr.mpr âŸ¨i, hiâŸ© },
  refine ((is_immersion_open_cover_tfae (f âˆ£_ V)).out 0 5).mpr âŸ¨_, U', this, Î» i, _âŸ©,
  refine (is_immersion_respects_iso.arrow_iso_iff (morphism_restrict_restrict f _ _ â‰ªâ‰«
    morphism_restrict_eq f ((V.functor_map_eq_inf _).trans _))).mpr (h i),
  rw inf_eq_left,
  exact le_supr U i,
end

lemma is_closed_immersion_pullback_diagonal_Spec {R S : CommRing}
  (f' : Scheme.Spec.obj (op S) âŸ¶ Scheme.Spec.obj (op R)) :
  is_closed_immersion (pullback.diagonal f') :=
begin
  let f : R âŸ¶ S := (Scheme.Spec.preimage f').unop,
  have : Scheme.Spec.map f.op = f', { rw [quiver.hom.op_unop, functor.image_preimage] },
  clear_value f, subst this,
  letI := f.to_algebra,
  have H := (is_pushout.of_is_colimit
    (CommRing.pushout_cocone_is_colimit f f)).op.map Scheme.Spec,
  let e : pullback.diagonal_obj (Scheme.Spec.map f.op) â‰… 
    Scheme.Spec.obj (op (CommRing.pushout_cocone f f).X) := limit.iso_limit_cone âŸ¨_, H.is_limitâŸ©,
  have : pullback.diagonal _ â‰« e.hom = Scheme.Spec.map
    (quiver.hom.op $ (@algebra.tensor_product.lmul' R S _ _ _).to_ring_hom),
  { apply pullback_cone.is_limit.hom_ext H.is_limit,
    { rw category.assoc,
      erw [is_limit.cone_point_unique_up_to_iso_hom_comp, pullback.diagonal_fst],
      rw [H.cone_fst, â† functor.map_comp, â† op_comp],
      transitivity Scheme.Spec.map (ğŸ™ _).op, { rw [op_id, category_theory.functor.map_id] },
      congr' 2, ext1, simp },
    { rw category.assoc,
      erw [is_limit.cone_point_unique_up_to_iso_hom_comp, pullback.diagonal_snd],
      rw [H.cone_snd, â† functor.map_comp, â† op_comp],
      transitivity Scheme.Spec.map (ğŸ™ _).op, { rw [op_id, category_theory.functor.map_id] },
      congr' 2, ext1, simp }, }, 
  rw [â† is_closed_immersion_respects_iso.cancel_right_is_iso _ e.hom, this,
    is_closed_immersion_Spec_iff],
  exact Î» x, âŸ¨x âŠ—â‚œ 1, (algebra.tensor_product.lmul'_apply_tmul _ _).trans (mul_one _)âŸ©,
end

-- -- move me
-- abbreviation Scheme.hom.lift_open (U : opens Y.carrier) (hU : set.range f.1.base âŠ† U.1) :
--   X âŸ¶ Y.restrict U.open_embedding := 
-- is_open_immersion.lift (Y.of_restrict U.open_embedding) f
--   (hU.trans $ subset_of_eq $ subtype.range_coe.symm)

-- lemma Scheme.hom.lift_open_of_restrict (U : opens Y.carrier) (hU : set.range f.1.base âŠ† U.1) :
--   X.of_restrict _ â‰« f.lift_open U hU = f âˆ£_ U :=  
-- begin
--   rw â† cancel_mono (Y.of_restrict U.open_embedding),
--   rw [category.assoc, is_open_immersion.lift_fac, morphism_restrict_Î¹],
-- end

variable {f}

variables (ğ’° : Scheme.open_cover.{u} Y) (ğ’± : âˆ€ i, Scheme.open_cover.{u} (pullback f (ğ’°.map i)))

def pullback.diagonal_cover :
  (pullback.diagonal_obj f).open_cover :=
(Scheme.pullback.open_cover_of_base ğ’° f f).bind
  (Î» i, Scheme.pullback.open_cover_of_left_right (ğ’± i) (ğ’± i) pullback.snd pullback.snd)

def pullback.diagonal_cover_diagonal :
  opens (pullback.diagonal_obj f).carrier :=
â¨† i : Î£ i, (ğ’± i).J, ((pullback.diagonal_cover ğ’° ğ’±).map âŸ¨i.1, i.2, i.2âŸ©).opens_range

-- move me
@[simps]
def pullback.triplet_mk {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (x : X.carrier) (y : Y.carrier)
  (h : f.1.base x = g.1.base y) : pullback.triplet f g :=
âŸ¨x, y, _, h, rflâŸ©

lemma pullback.diagonal_cover_map_eq (I) : (pullback.diagonal_cover ğ’° ğ’±).map I =
  pullback.map _ _ _ _
    ((ğ’± I.fst).map _ â‰« pullback.fst) ((ğ’± I.fst).map _ â‰« pullback.fst) (ğ’°.map _)
    (by simp only [category.assoc, pullback.condition])
    (by simp only [category.assoc, pullback.condition]) :=
begin
  dsimp [pullback.diagonal_cover],
  apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc,
      pullback.diagonal_fst_assoc, pullback.diagonal_fst,
      pullback.diagonal_snd_assoc, pullback.diagonal_snd, category.comp_id]
end

lemma pullback.diagonal_cover_diagonal_eq_top_of_injective (hf : function.injective f.1.base) :
  pullback.diagonal_cover_diagonal ğ’° ğ’± = âŠ¤ :=
begin
  rw eq_top_iff,
  rintro x -, 
  let xâ‚ := (pullback.fst : pullback.diagonal_obj f âŸ¶ X).1.base x,
  let xâ‚‚ := (pullback.snd : pullback.diagonal_obj f âŸ¶ X).1.base x,
  have hx : xâ‚ = xâ‚‚,
  { apply hf, simp_rw [â† Scheme.comp_val_base_apply, pullback.condition] },
  let i := ğ’°.f (f.1.base xâ‚),
  obtain âŸ¨t, htâŸ© : xâ‚ âˆˆ set.range (pullback.fst : pullback f (ğ’°.map i) âŸ¶ _).1.base,
  { rw is_open_immersion.range_pullback_fst_of_right, exact (ğ’°.covers $ f.1.base xâ‚) },
  obtain âŸ¨w, hwâŸ© := (ğ’± i).covers t,
  rw â† hw at ht,
  refine opens.mem_supr.mpr âŸ¨âŸ¨i, (ğ’± i).f tâŸ©, _âŸ©,
  change _ âˆˆ set.range ((pullback.diagonal_cover ğ’° ğ’±).map âŸ¨i, ((ğ’± i).f t, (ğ’± i).f t)âŸ©).1.base,
  simp_rw [pullback.diagonal_cover_map_eq ğ’° ğ’±, pullback.range_map],
  split,
  { exact âŸ¨w, htâŸ© },
  { rw hx at ht, exact âŸ¨w, htâŸ© }
end
  
lemma pullback.diagonal_range_subset_supr_diagonal_cover :
  set.range (pullback.diagonal f).1.base âŠ† (pullback.diagonal_cover_diagonal ğ’° ğ’±).1 :=
begin
  rintros _ âŸ¨x, rflâŸ©,
  let i := ğ’°.f (f.1.base x),
  obtain âŸ¨y, hy : (ğ’°.map i).1.base y = _âŸ© := ğ’°.covers (f.1.base x),
  let T : pullback.triplet f (ğ’°.map i) := âŸ¨x, y, _, rfl, hyâŸ©,
  obtain âŸ¨z, (hzx : _ = x), (rfl : _ = y)âŸ© := T.exists_preimage,
  obtain âŸ¨w, hwâŸ© := (ğ’± i).covers z,
  rw [â† hzx, â† hw],
  refine opens.mem_supr.mpr âŸ¨âŸ¨i, (ğ’± i).f zâŸ©,
    (pullback.diagonal ((ğ’± i).map ((ğ’± i).f z) â‰« pullback.snd)).1.base w, _âŸ©,
  simp_rw [â† Scheme.comp_val_base_apply],
  congr' 3,
  dsimp [pullback.diagonal_cover],
  apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc,
      pullback.diagonal_fst_assoc, pullback.diagonal_fst,
      pullback.diagonal_snd_assoc, pullback.diagonal_snd, category.comp_id],
end

def pullback.diagonal_restrict_iso_diagonal (i j) :
  arrow.mk (pullback.diagonal f âˆ£_ ((pullback.diagonal_cover ğ’° ğ’±).map âŸ¨i, j, jâŸ©).opens_range) â‰…
    arrow.mk (pullback.diagonal ((ğ’± i).map j â‰« pullback.snd)) :=
begin
  refine (morphism_restrict_opens_range _ _) â‰ªâ‰« arrow.iso_mk _ _ _,
  { dsimp [pullback.diagonal_cover], 
    refine pullback.congr_hom rfl _ â‰ªâ‰«
      pullback_diagonal_map_iso f (ğ’°.map i) ((ğ’± i).map j) ((ğ’± i).map j) â‰ªâ‰«
      as_iso pullback.fst,
    apply pullback.hom_ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
        pullback.lift_fst_assoc, pullback.lift_snd_assoc],
      },
  { dsimp [pullback.diagonal_cover], exact iso.refl _ },
  { have : (pullback.fst : pullback ((ğ’± i).map j) ((ğ’± i).map j) âŸ¶ _) = pullback.snd,
    { rw â† cancel_epi (pullback.diagonal $ (ğ’± i).map j),
      rw [pullback.diagonal_fst, pullback.diagonal_snd] },
    dsimp [pullback.diagonal_cover],
    apply pullback.hom_ext,
    swap, simp only [this],
    all_goals { simp only [category.assoc, pullback.lift_fst, pullback.lift_snd,  
      pullback.lift_fst_assoc, pullback.lift_snd_assoc, category.id_comp, category.comp_id,
      pullback_diagonal_map_iso_hom_fst, pullback_diagonal_map_iso_hom_snd,
      pullback.diagonal_fst, pullback.diagonal_snd] } }
end

lemma pullback.is_closed_immersion_lift_diagonal_aux
  (H : âˆ€ i j, is_closed_immersion (pullback.diagonal ((ğ’± i).map j â‰« pullback.snd))) :
  is_closed_immersion (pullback.diagonal f âˆ£_ pullback.diagonal_cover_diagonal ğ’° ğ’±) :=
begin
  rw (is_closed_immersion.open_cover_tfae (pullback.diagonal
    f âˆ£_ pullback.diagonal_cover_diagonal ğ’° ğ’±)).out 0 5,
  have : (â¨† i : Î£ i, (ğ’± i).J, ((pullback.diagonal_cover ğ’° ğ’±).map âŸ¨i.1, i.2, i.2âŸ©).opens_range) =
    pullback.diagonal_cover_diagonal ğ’° ğ’± := rfl,
  apply_fun (opens.map (pullback.diagonal_cover_diagonal ğ’° ğ’±).inclusion).obj at this,
  rw [opens.map_supr, opens.inclusion_map_eq_top] at this,
  refine âŸ¨_, _, this, _âŸ©,
  rintros âŸ¨i, jâŸ©,
  rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (morphism_restrict_restrict _ _ _),
  rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (morphism_restrict_eq _ $ (opens.functor_map_eq_inf _ _).trans (inf_eq_left.mpr _)),
  { rw is_closed_immersion_respects_iso.arrow_mk_iso_iff
    (pullback.diagonal_restrict_iso_diagonal ğ’° ğ’± i j), apply H },
  { exact le_supr _ _ }
end

variable (f)

lemma pullback.is_closed_immersion_lift_diagonal :
  is_closed_immersion (pullback.diagonal f âˆ£_ pullback.diagonal_cover_diagonal
    Y.affine_cover (Î» _, Scheme.affine_cover _)) :=
begin
  apply pullback.is_closed_immersion_lift_diagonal_aux,
  intros i j, apply is_closed_immersion_pullback_diagonal_Spec
end


local attribute [irreducible] Scheme.affine_cover

def Scheme.restrict_top_iso (X : Scheme.{u}) : 
  X.restrict (opens.open_embedding $ âŠ¤) â‰… X :=
{ .. X.to_LocallyRingedSpace.restrict_top_iso }

-- Declaring it as an instance adds superfluous universe variables
@[instance]
lemma _root_.category_theory.limits.pullback.diagonal.is_immersion {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  is_immersion (pullback.diagonal f) :=
begin
  let U := (pullback.diagonal_cover_diagonal Y.affine_cover (Î» _, Scheme.affine_cover _)),
  haveI : is_closed_immersion (pullback.diagonal f âˆ£_ U) :=
    pullback.is_closed_immersion_lift_diagonal f,
  have : (opens.map (pullback.diagonal f).val.base).obj U = âŠ¤,
  { ext1, show (pullback.diagonal f).val.base â»Â¹' U.1 = set.univ,
    rw [â† set.univ_subset_iff, â† set.image_subset_iff, set.image_univ],
    exact pullback.diagonal_range_subset_supr_diagonal_cover _ _ },
  haveI : is_iso (X.of_restrict ((opens.map (pullback.diagonal f).val.base).obj U).open_embedding),
  { rw this, exact is_iso.of_iso X.restrict_top_iso },
  have := morphism_restrict_Î¹ (pullback.diagonal f) U,
  rw [â† is_iso.inv_comp_eq] at this,
  rw â† this,
  apply_instance
end
-- move me 
def morphism_restrict.top_iso : arrow.mk (f âˆ£_ âŠ¤) â‰… arrow.mk f :=
arrow.iso_mk' (f âˆ£_ âŠ¤) f X.restrict_top_iso Y.restrict_top_iso (morphism_restrict_Î¹ _ _).symm

-- Also see `separated_of_injective`
lemma pullback.diagonal_is_closed_immersion_of_injective {X Y : Scheme.{u}} (f : X âŸ¶ Y)
  (hf : function.injective f.1.base) :
  is_closed_immersion (pullback.diagonal f) :=
begin
  let U := (pullback.diagonal_cover_diagonal Y.affine_cover (Î» _, Scheme.affine_cover _)),
  have hU : is_closed_immersion (pullback.diagonal f âˆ£_ U) :=
    pullback.is_closed_immersion_lift_diagonal f,
  have : U = âŠ¤ := pullback.diagonal_cover_diagonal_eq_top_of_injective _ _ hf,
  rw this at hU,
  exact (is_closed_immersion_respects_iso.arrow_mk_iso_iff (morphism_restrict.top_iso _)).mp hU
end

end algebraic_geometry
