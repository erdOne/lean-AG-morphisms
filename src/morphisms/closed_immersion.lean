/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import morphisms.ring_hom_properties
import morphisms.monomorphism
import topology.sheaves.locally_surjective
import topology.local_at_target
import ring_theory.ring_hom.surjective
import algebraic_geometry.stalk_inducing
import for_mathlib.module_local_property
import algebraic_geometry.pushforward_stalk

/-!

# Closed immersions

A morphism of schemes is a closed immersion if the underlying map is a closed embedding, and 
the sheaf map is locally surjective.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)

/-- A morphism is a `is_closed_immersion` if the preimages of affine open sets are affine. -/
@[mk_iff]
class is_closed_immersion (f : X ‚ü∂ Y) : Prop :=
(base_closed [] : closed_embedding f.1.base)
(c_locally_surjective [] : Top.presheaf.is_locally_surjective f.1.c)

lemma _root_.Top.presheaf.stalk_pushforward_subsingleton
  {X Y : Top} (F : X.presheaf CommRing) (hF : F.is_sheaf) (f : X ‚ü∂ Y) (x : Y)
    (hx : x ‚àâ closure (set.range f)) :
  subsingleton ((f _* F).stalk x) :=
begin
  apply subsingleton_of_forall_eq (0 : (f _* F).stalk x),
  intro s,
  obtain ‚ü®U, hxU, s', e‚ü© := (f _* F).germ_exist _ s,
  let V : opens Y := U ‚äì ‚ü®(closure (set.range f))·∂ú, is_closed_closure.is_open_compl‚ü©,
  let t : (f _* F).obj (op V) := (f _* F).map (hom_of_le inf_le_left).op s',
  have hxV : x ‚àà V := ‚ü®hxU, hx‚ü©,
  obtain rfl : (f _* F).germ ‚ü®x, hxV‚ü© t = s,
  { rw [Top.presheaf.germ_res_apply, ‚Üê e], refl },
  haveI : subsingleton ((f _* F).obj (op V)),
  { change subsingleton (F.obj $ op $ (opens.map f).obj V),
    refine CommRing.subsingleton_of_is_terminal
      (Top.sheaf.is_terminal_of_eq_empty ‚ü®F, hF‚ü© $ eq_bot_iff.mpr _),
    exact Œª x hx, hx.2 (subset_closure ‚ü®x, rfl‚ü©) },
  rw [subsingleton.elim t 0, map_zero]
end

lemma is_closed_immersion_iff_stalk {f : X ‚ü∂ Y} :
  is_closed_immersion f ‚Üî
    closed_embedding f.1.base ‚àß ‚àÄ x, function.surjective (PresheafedSpace.stalk_map f.1 x) :=
begin
  rw [is_closed_immersion_iff, Top.presheaf.locally_surjective_iff_surjective_on_stalks],
  delta PresheafedSpace.stalk_map,
  split,
  { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, refine ‚ü®h‚ÇÅ, Œª x, function.surjective.comp _ (h‚ÇÇ $ f.1.base x)‚ü©,
    haveI := X.presheaf.stalk_pushforward_iso_of_inducing h‚ÇÅ.to_inducing x,
    exact (as_iso $ X.presheaf.stalk_pushforward CommRing f.1.base x)
      .CommRing_iso_to_ring_equiv.surjective },
  { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, refine ‚ü®h‚ÇÅ, Œª y, _‚ü©,
    by_cases y ‚àà set.range f.1.base,
    { obtain ‚ü®x, rfl‚ü© := h, 
      haveI := X.presheaf.stalk_pushforward_iso_of_inducing h‚ÇÅ.to_inducing x,
      have := (as_iso $ X.presheaf.stalk_pushforward CommRing f.1.base x).symm
        .CommRing_iso_to_ring_equiv.surjective.comp (h‚ÇÇ x),
      erw ‚Üê coe_comp at this,
      simpa using this },
    { intro s,
      rw ‚Üê closure_eq_iff_is_closed.mpr h‚ÇÅ.closed_range at h,
      have := X.presheaf.stalk_pushforward_subsingleton X.sheaf.2 f.1.base y h,
      rw @@subsingleton.elim this s 0,
      exact ‚ü®0, map_zero _‚ü© } }
end

lemma is_closed_immersion.stalk_map_surjective [is_closed_immersion f] (x : X.carrier) :
  function.surjective (PresheafedSpace.stalk_map f.1 x) :=
(is_closed_immersion_iff_stalk.mp infer_instance).2 x

instance is_closed_immersion_of_is_iso (f : X ‚ü∂ Y) [is_iso f] : is_closed_immersion f :=
begin
  refine is_closed_immersion_iff_stalk.mpr ‚ü®(Top.homeo_of_iso $ as_iso f.1.base).closed_embedding, _‚ü©,
  intro x,
  exact ((forget _).map_iso (as_iso $ PresheafedSpace.stalk_map f.val x)).to_equiv.surjective,
end

lemma is_closed_immersion_stable_under_composition :
  morphism_property.stable_under_composition @is_closed_immersion :=
begin
  introsI X Y Z f g h‚ÇÅ h‚ÇÇ,
  rw is_closed_immersion_iff_stalk at h‚ÇÅ h‚ÇÇ ‚ä¢,
  refine ‚ü®h‚ÇÇ.1.comp h‚ÇÅ.1, Œª x, _‚ü©,
  erw PresheafedSpace.stalk_map.comp,
  exact (h‚ÇÅ.2 x).comp (h‚ÇÇ.2 $ f.1 x)
end

lemma is_closed_immersion_respects_iso :
  morphism_property.respects_iso @is_closed_immersion :=
begin
  apply is_closed_immersion_stable_under_composition.respects_iso,
  intros _ _ _, apply_instance
end

lemma is_closed_immersion_is_local_at_target : property_is_local_at_target @is_closed_immersion :=
begin
  constructor,
  { exact is_closed_immersion_respects_iso },
  { simp_rw is_closed_immersion_iff_stalk,
    rintros X Y f U ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    split,
    { rw morphism_restrict_val_base, exact h‚ÇÅ.restrict_preimage U.1 },
    { exact Œª x, ((morphism_property.surjective_respects_iso _).arrow_iso_iff
      (morphism_restrict_stalk_map f U x)).mpr (h‚ÇÇ x.1) } },
  { intros X Y f ùí∞ H,
    rw is_closed_immersion_iff_stalk,
    split,
    { apply (closed_embedding_iff_closed_embedding_of_supr_eq_top
        ùí∞.supr_opens_range f.1.base.2).mpr,
      intro i,
      have := ((is_closed_immersion_respects_iso.arrow_iso_iff
        (morphism_restrict_opens_range f (ùí∞.map i))).mpr (H i)).1,
      rwa [arrow.mk_hom, morphism_restrict_val_base] at this },
    { exact Œª x, ((morphism_property.surjective_respects_iso _).arrow_iso_iff
        (morphism_restrict_stalk_map f _ _)).mp ((is_closed_immersion_iff_stalk.mp $
        (is_closed_immersion_respects_iso.arrow_iso_iff (morphism_restrict_opens_range f (ùí∞.map _)))
        .mpr (H (ùí∞.f $ f.1.base x))).2 ‚ü®x, ùí∞.covers (f.1.base x)‚ü©) } }
end

lemma is_affine_of_closed_embedding {X Y : Scheme} (f : X ‚ü∂ Y) [is_affine Y]
  (hf : closed_embedding f.1.base) : is_affine X :=
begin
  have : ‚àÄ x, ‚àÉ (s : Y.presheaf.obj (op ‚ä§)) (U : X.affine_opens),
    f.1.base x ‚àà Y.basic_open s ‚àß @Scheme.basic_open X ‚ä§ (f.1.c.app (op ‚ä§) s) ‚â§ U.1,
  { intro x,
    obtain ‚ü®_, ‚ü®U, hU : is_affine_open U, rfl‚ü©, hxU, -‚ü© :=
      (is_basis_affine_open X).exists_subset_of_mem_open
      (show x ‚àà (set.univ : set X.carrier), from trivial) is_open_univ,
    obtain ‚ü®V, hV, hV'‚ü© := hf.to_inducing.is_open_iff.mp U.prop,
    rw ‚Üê hV' at hxU,
    obtain ‚ü®_, ‚ü®_, ‚ü®r, rfl‚ü©, rfl‚ü©, hxr, hr‚ü© :=
      (is_basis_basic_open Y).exists_subset_of_mem_open (show f.1.base x ‚àà _, from hxU) hV,
    refine ‚ü®r, ‚ü®U, hU‚ü©, hxr, _‚ü©,
    rw ‚Üê Scheme.preimage_basic_open,
    have := set.preimage_mono hr, rw hV' at this, exact this },
  choose s U hs hU using this,
  have := hf.closed_range,
  obtain ‚ü®Œπ, t, ht, ht'‚ü© := (is_basis_basic_open Y).open_eq_Union hf.closed_range.1,
  have : ‚àÄ i : Œπ, ‚àÉ r : Y.presheaf.obj (op ‚ä§), (Y.basic_open r).1 = t i,
  { intro i, rcases ht' i with ‚ü®_, ‚ü®r, rfl‚ü©, e‚ü©, exact ‚ü®r, e‚ü© },
  choose t' ht' using this,
  let r : X.carrier ‚äï Œπ ‚Üí Y.presheaf.obj (op ‚ä§) := sum.elim s t',
  apply is_affine_of_span_top_of_is_affine_open X (f.1.c.app (op ‚ä§) '' set.range r),
  { rw [‚Üê ideal.map_span, ‚Üê ideal.map_top (f.1.c.app (op ‚ä§))],
    congr' 1,
    rw [‚Üê (top_is_affine_open Y).basic_open_union_eq_self_iff, eq_top_iff],
    rintro x -,
    erw opens.mem_supr,
    by_cases x ‚àà set.range f.1.base,
    { obtain ‚ü®x, rfl‚ü© := h, exact ‚ü®‚ü®_, sum.inl _, rfl‚ü©, hs _‚ü© },
    { rw [‚Üê set.mem_compl_iff, ht, set.mem_Union] at h,
      obtain ‚ü®i, hi‚ü© := h, rw ‚Üê ht' at hi, exact ‚ü®‚ü®_, sum.inr _, rfl‚ü©, hi‚ü© } },
  { rintro ‚ü®_, _, ‚ü®i|i, rfl‚ü©, rfl‚ü©; dsimp only [r, sum.elim],
      have := inf_eq_right.mpr (hU i),
      { convert (U i).2.basic_open_is_affine
          (X.presheaf.map (hom_of_le le_top).op $ (f.val.c.app (op ‚ä§)) (s i)) using 1,
        rw Scheme.basic_open_res,
        exact (inf_eq_right.mpr (hU i)).symm },
      { convert bot_is_affine_open X,
        rw ‚Üê Scheme.preimage_basic_open,
        ext1,
        refine @set.preimage_eq_empty _ _ f.1.base _ _,
        apply set.subset_compl_iff_disjoint_right.mp,
        rw [ht', ht],
        exact set.subset_Union _ _ } }
end

lemma is_closed_immersion_le_affine : 
  @is_closed_immersion ‚â§ @affine :=
begin
  rw [‚Üê is_closed_immersion_is_local_at_target.target_affine_locally_eq, affine_eq_affine_property],
  refine target_affine_locally_mono _,
  introsI X Y f H hf,
  exact is_affine_of_closed_embedding f hf.1,
end

instance is_closed_immersion.to_affine [H : is_closed_immersion f] : affine f :=
is_closed_immersion_le_affine X Y f H

lemma surjective_of_is_closed_immersion {R S : CommRing} (f : R ‚ü∂ S)
  [is_closed_immersion (Scheme.Spec.map f.op)] : function.surjective f :=
begin
  letI := f.to_algebra,
  change function.surjective (algebra.of_id R S).to_linear_map,
  apply linear_map.surjective_of_localization_maximal,
  introsI m hm,
  convert_to function.surjective ((localized_module.map m.prime_compl
      (algebra.of_id R S).to_linear_map).restrict_scalars R),
  rw Spec.localized_module_map_iso_stalk_map' R S ‚ü®m, _‚ü©,
  apply function.surjective.comp,
  { exact (linear_equiv.to_equiv _).surjective },
  dsimp only,
  apply function.surjective.comp,
  { exact (Top.presheaf.locally_surjective_iff_surjective_on_stalks _).mp
      (is_closed_immersion.c_locally_surjective (Scheme.Spec.map f.op) : _) ‚ü®m, _‚ü© },
  { exact (linear_equiv.to_equiv _).surjective },
end

lemma property_le_of_le_affine {P‚ÇÅ P‚ÇÇ : morphism_property Scheme}
  (h‚ÇÅ : P‚ÇÅ ‚â§ @affine)
  (h‚ÇÅ' : property_is_local_at_target P‚ÇÅ) (h‚ÇÇ' : property_is_local_at_target P‚ÇÇ) 
  (h : ‚àÄ {R S : CommRing} (f : R ‚ü∂ S), P‚ÇÅ (Scheme.Spec.map f.op) ‚Üí P‚ÇÇ (Scheme.Spec.map f.op)) :
  P‚ÇÅ ‚â§ P‚ÇÇ :=
begin
  rw [‚Üê h‚ÇÅ'.target_affine_locally_eq, ‚Üê h‚ÇÇ'.target_affine_locally_eq],
  apply target_affine_locally_mono,
  intros X Y f hX hf,
  haveI := h‚ÇÅ _ _ _ hf,
  haveI := is_affine_of_affine f,
  have := Œì_Spec.adjunction.unit_naturality f,
  rw [‚Üê h‚ÇÇ'.respects_iso.cancel_right_is_iso f (Œì_Spec.adjunction.unit.app Y),
    ‚Üê Œì_Spec.adjunction.unit_naturality f, h‚ÇÇ'.respects_iso.cancel_left_is_iso],
  rw [‚Üê h‚ÇÅ'.respects_iso.cancel_right_is_iso f (Œì_Spec.adjunction.unit.app Y),
    ‚Üê Œì_Spec.adjunction.unit_naturality f, h‚ÇÅ'.respects_iso.cancel_left_is_iso] at hf,
  exact h (Scheme.Œì.map f.op) hf
end

lemma property_ext_of_le_affine {P‚ÇÅ P‚ÇÇ : morphism_property Scheme}
  (h‚ÇÅ : P‚ÇÅ ‚â§ @affine) (h‚ÇÇ : P‚ÇÇ ‚â§ @affine)
  (h‚ÇÅ' : property_is_local_at_target P‚ÇÅ) (h‚ÇÇ' : property_is_local_at_target P‚ÇÇ) 
  (h : ‚àÄ {R S : CommRing} (f : R ‚ü∂ S), P‚ÇÅ (Scheme.Spec.map f.op) ‚Üî P‚ÇÇ (Scheme.Spec.map f.op)) :
  P‚ÇÅ = P‚ÇÇ :=
begin
  refine (property_le_of_le_affine h‚ÇÅ h‚ÇÅ' h‚ÇÇ' _).antisymm (property_le_of_le_affine h‚ÇÇ h‚ÇÇ' h‚ÇÅ' _);
    simp only [h, forall_3_true_iff, imp_self]
end

def is_closed_immersion.affine_property : affine_target_morphism_property :=
affine_and $ Œª R S _ _ f, function.surjective f

-- move me
lemma _root_.ring_hom.localization_surjective : 
  ring_hom.localization_preserves (Œª R S _ _ f, function.surjective ‚áëf) :=
begin
  introsI R S _ _ f M R' S' _ _ _ _ _ _ H x,
  obtain ‚ü®x, ‚ü®_, ‚ü®s, hs, rfl‚ü©‚ü©, rfl‚ü© := is_localization.mk'_surjective (M.map f) x,
  obtain ‚ü®x, rfl‚ü© := H x,
  exact ‚ü®is_localization.mk' R' x ‚ü®s, hs‚ü©, is_localization.map_mk' _ _ _‚ü©
end

lemma is_closed_immersion.affine_property_is_local :
  (is_closed_immersion.affine_property : _).is_local :=
is_local_affine_and _
  ring_hom.surjective_respects_iso
  ring_hom.localization_surjective
  ring_hom.surjective_of_localization_span

lemma is_closed_immersion_eq_affine_property : 
  @is_closed_immersion = target_affine_locally is_closed_immersion.affine_property :=
begin
  apply property_ext_of_le_affine is_closed_immersion_le_affine
    (by exact (target_affine_locally_affine_and_le_affine _)) is_closed_immersion_is_local_at_target
    is_closed_immersion.affine_property_is_local.target_affine_locally_is_local,
  simp_rw [is_closed_immersion.affine_property_is_local.affine_target_iff,
    is_closed_immersion.affine_property, affine_and_Spec_iff ring_hom.surjective_respects_iso],
  intros R S œï,
  refine ‚ü®Œª h, by exactI surjective_of_is_closed_immersion œï, _‚ü©,
  { introI h,
    refine ‚ü®prime_spectrum.closed_embedding_comap_of_surjective _ _ h,
      (Top.presheaf.locally_surjective_iff_surjective_on_stalks _).mpr _‚ü©,
    rintro (x : prime_spectrum R),
    letI := œï.to_algebra,
    convert_to function.surjective (is_scalar_tower.to_alg_hom R
      ((Spec.structure_sheaf R).presheaf.stalk x) 
      ((Spec.Top_map (algebra_map R S) _* (Spec.structure_sheaf S).1).stalk x)).to_linear_map,
    have := Spec.localized_module_map_iso_stalk_map R S x,
    refine @function.surjective.of_comp _ _ _ _ ((is_localized_module.iso x.as_ideal.prime_compl
      (algebra.of_id R ((Spec.structure_sheaf R).presheaf.stalk x)).to_linear_map).to_linear_map) _,
    rw [‚Üê linear_map.coe_comp, ‚Üê this],
    apply function.surjective.comp,
    { exact (linear_equiv.to_equiv _).surjective },
    { exact linear_map.surjective_localized_module_map _ (algebra.of_id R S).to_linear_map h } }
end

lemma is_closed_immersion.affine_open_cover_tfae {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
  tfae [is_closed_immersion f,
    ‚àÉ (ùí∞ : Scheme.open_cover.{u} Y) [‚àÄ i, is_affine (ùí∞.obj i)],
      ‚àÄ (i : ùí∞.J), is_affine (pullback f (ùí∞.map i)) ‚àß
        function.surjective (Scheme.Œì.map (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _).op),
    ‚àÄ (ùí∞ : Scheme.open_cover.{u} Y) [‚àÄ i, is_affine (ùí∞.obj i)] (i : ùí∞.J),
      is_affine (pullback f (ùí∞.map i)) ‚àß
        function.surjective (Scheme.Œì.map (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _).op),
    ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [is_affine U] [is_open_immersion g],
      is_affine (pullback f g) ‚àß
        function.surjective (Scheme.Œì.map (pullback.snd : pullback f g ‚ü∂ _).op),
    ‚àÉ {Œπ : Type u} (U : Œπ ‚Üí opens Y.carrier) (hU : supr U = ‚ä§) (hU' : ‚àÄ i, is_affine_open (U i)),
      ‚àÄ i, is_affine_open ((opens.map f.1.base).obj $ U i) ‚àß
        function.surjective (f.1.c.app (op $ U i))] :=
begin
  rw is_closed_immersion_eq_affine_property,
  convert is_closed_immersion.affine_property_is_local.affine_open_cover_tfae f,
  delta is_closed_immersion.affine_property,
  ext Œπ,
  refine exists‚ÇÉ_congr (Œª U hU hU', forall_congr $ Œª i, and_congr iff.rfl _),
  dsimp only,
  rw [algebraic_geometry.Scheme.Œì_map, quiver.hom.unop_op, morphism_restrict_c_app, coe_comp],
  refine iff.trans _ (function.surjective.of_comp_iff' _ _).symm,
  { let V := (U i).open_embedding.is_open_map.functor.obj ‚ä§,
    show _ ‚Üî function.surjective (f.1.c.app (op V)),
    have e : V = _ := (U i).open_embedding_obj_top, clear_value V, subst e },
  { exact (as_iso $ X.presheaf.map (eq_to_hom _).op).CommRing_iso_to_ring_equiv.bijective }
end

lemma is_closed_immersion.is_local_at_target :
  property_is_local_at_target @is_closed_immersion :=
is_closed_immersion_eq_affine_property.symm ‚ñ∏
  is_closed_immersion.affine_property_is_local.target_affine_locally_is_local

lemma is_closed_immersion.open_cover_tfae {X Y : Scheme.{u}} (f : X ‚ü∂ Y) :
  tfae [is_closed_immersion f,
    ‚àÉ (ùí∞ : Scheme.open_cover.{u} Y), ‚àÄ (i : ùí∞.J),
      is_closed_immersion (pullback.snd : (ùí∞.pullback_cover f).obj i ‚ü∂ ùí∞.obj i),
    ‚àÄ (ùí∞ : Scheme.open_cover.{u} Y) (i : ùí∞.J),
      is_closed_immersion (pullback.snd : (ùí∞.pullback_cover f).obj i ‚ü∂ ùí∞.obj i),
    ‚àÄ (U : opens Y.carrier), is_closed_immersion (f ‚à£_ U),
    ‚àÄ {U : Scheme} (g : U ‚ü∂ Y) [is_open_immersion g],
      is_closed_immersion (pullback.snd : pullback f g ‚ü∂ _),
    ‚àÉ {Œπ : Type u} (U : Œπ ‚Üí opens Y.carrier) (hU : supr U = ‚ä§), ‚àÄ i, is_closed_immersion (f ‚à£_ (U i))] :=
is_closed_immersion_eq_affine_property.symm ‚ñ∏
  is_closed_immersion.affine_property_is_local.target_affine_locally_is_local.open_cover_tfae f

lemma is_closed_immersion_over_affine_iff {X Y : Scheme} (f : X ‚ü∂ Y) [is_affine Y] :
  is_closed_immersion f ‚Üî is_affine X ‚àß function.surjective (Scheme.Œì.map f.op) :=
is_closed_immersion_eq_affine_property.symm ‚ñ∏
  is_closed_immersion.affine_property_is_local.affine_target_iff f

lemma is_closed_immersion_Spec_iff {R S : CommRing} (f : R ‚ü∂ S) :
  is_closed_immersion (Scheme.Spec.map f.op) ‚Üî function.surjective f :=
begin
  rw [is_closed_immersion_eq_affine_property,
    is_closed_immersion.affine_property_is_local.affine_target_iff,
    is_closed_immersion.affine_property, affine_and_Spec_iff ring_hom.surjective_respects_iso]
end

local attribute [instance] mono_comp

lemma is_closed_immersion_le_mono : @is_closed_immersion ‚â§ @mono Scheme _ :=
begin
  rw [‚Üê is_closed_immersion_is_local_at_target.target_affine_locally_eq,
    ‚Üê mono_is_local_at_target.target_affine_locally_eq],
  apply target_affine_locally_mono,
  introsI X Y f _ H,
  haveI := is_affine_of_affine f,
  rw is_closed_immersion_over_affine_iff at H,
  haveI := (forget CommRing).epi_of_epi_map ((epi_iff_surjective _).mpr H.2),
  have := Œì_Spec.adjunction.unit_naturality f,
  rw [functor.right_op_map, ‚Üê is_iso.comp_inv_eq] at this,
  rw ‚Üê this,
  apply_instance
end

@[priority 100]
instance is_closed_immersion.to_mono [is_closed_immersion f] : mono f :=
is_closed_immersion_le_mono _ _ f infer_instance

lemma is_closed_immersion.affine_open_cover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.open_cover.{u} Y)
  [‚àÄ i, is_affine (ùí∞.obj i)] (f : X ‚ü∂ Y) :
  is_closed_immersion f ‚Üî ‚àÄ i, is_affine (pullback f (ùí∞.map i)) ‚àß
        function.surjective (Scheme.Œì.map (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _).op) :=
is_closed_immersion_eq_affine_property.symm ‚ñ∏
  is_closed_immersion.affine_property_is_local.affine_open_cover_iff f ùí∞

lemma is_closed_immersion.open_cover_iff {X Y : Scheme.{u}} (ùí∞ : Scheme.open_cover.{u} Y) (f : X ‚ü∂ Y) :
  is_closed_immersion f ‚Üî ‚àÄ i, is_closed_immersion (pullback.snd : pullback f (ùí∞.map i) ‚ü∂ _) :=
is_closed_immersion_eq_affine_property.symm ‚ñ∏
  is_closed_immersion.affine_property_is_local.target_affine_locally_is_local.open_cover_iff f ùí∞

instance [is_closed_immersion f] [is_closed_immersion g] : is_closed_immersion (f ‚â´ g) :=
is_closed_immersion_stable_under_composition _ _ infer_instance infer_instance

lemma is_closed_immersion_stable_under_base_change : 
  morphism_property.stable_under_base_change @is_closed_immersion := 
begin
  rw is_closed_immersion_eq_affine_property,
  exact affine_and_stable_under_base_change _ ring_hom.surjective_respects_iso
    ring_hom.localization_surjective ring_hom.surjective_of_localization_span
    ring_hom.surjective_stable_under_base_change,
end

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [is_closed_immersion g] :
  is_closed_immersion (pullback.fst : pullback f g ‚ü∂ X) :=
is_closed_immersion_stable_under_base_change.fst f g infer_instance

instance (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [is_closed_immersion f] :
  is_closed_immersion (pullback.snd : pullback f g ‚ü∂ Y) :=
is_closed_immersion_stable_under_base_change.snd f g infer_instance

end algebraic_geometry

